# !/bin/csh -f
#
# Installs HADB on a clean system.
#
# This file is meant to be run as a CSH script, or used as
# instructions.  READ ALL OF THE NOTES AND REVIEW THE STEPS BEFORE
# PROCEEDING.
#
# This script assumes that the tarballs are in a subdirectory (see
# below).  If they are elsewhere, you'll need to hack it.
#
# The order in which the tarballs are applied apparently matters.  If
# this is true then this might fail because it attempts to install
# EVERYTHING, everywhere, and this is not the usual usage.

# Variables to configure:
#
# [1] The OWNER.  HADB can be run/administered as root, but the
# recommendation is to let the files be owned by a designated user. 
# This can be a normal user, or a special account set up for only this
# purpose.  It must be valid on all of the HADB hosts.  It should also
# be set up so that it can log in without password from any of the
# hosts to any other (i.e., via .rhosts), which precludes root.  [This
# requirement is mentioned in the older docs but not the newer:  maybe
# it isn't necessary any more.]
#
# [2] The VERSION of HADB.  Set the exact version (according the
# labels on the tarballs) of HADB that you plan to install.
#
# Other variables include H (the hosttype) and O (the ostype).  These
# should be automatically detected and configured, so you shouldn't
# need to futz with them.

# For the ETC installation:
#set OWNER	= de153050
#set VERSION	= 4.4.3-2

set OWNER	= ChangeMe
set VERSION	= ChangeMe

if ($HOSTTYPE == "sun4") then
	set H = sparc
else if ($HOSTTYPE == "i86pc") then
	set H = ix86
else
	echo "Unknown HOSTTYPE"
	exit 1
endif

if ($OSTYPE == "solaris") then
	set O = solaris
else if ($OSTYPE == "linux") then
	set O = linux
else
	echo "Unknown OSTYPE"
	exit 1
endif

foreach tb ( c x a s j m o e v i )
    if (! -f Tarballs/SUNWhadb$tb-$VERSION.$H.$O.tar.gz) then
	    echo "Missing version ($VERSION.$H.$O) for SUNWhadb$tb"
	    echo "Please check that you have all the tarballs."
	    exit 1
    endif
end

# NOTE the directories:  these are the defaults.  Make sure they're
# what you really want.

gunzip -c Tarballs/SUNWhadbc-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadbx-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadba-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadbs-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadbj-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadbm-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)
gunzip -c Tarballs/SUNWhadbo-$VERSION.$H.$O.tar.gz | (cd /opt; tar xf -)

gunzip -c Tarballs/SUNWhadbe-$VERSION.$H.$O.tar.gz | (cd /etc/opt; tar xf -)
gunzip -c Tarballs/SUNWhadbv-$VERSION.$H.$O.tar.gz | (cd /var/opt; tar xf -)
gunzip -c Tarballs/SUNWhadbi-$VERSION.$H.$O.tar.gz | (cd /; tar xf -)

# NOTE the assumption about the relationship between the machine names
# and the name of the repository.  Make whatever changes are
# necessary:  unless you always call your HADB nodes "darkstar[0-9]*",
# you'll need to change this in some manner!  Whatever you plan to put
# into the ma.cfg file should be reflected here.

set thisHost = `hostname | sed -e 's/[0-9].*//'`
if ($thisHost == "darkstar") then
    if (! -d /u0/HADB-repos/) then
	mkdir -p /u0/HADB-repos/darkstar
    endif
endif

chown -R $OWNER /opt/SUNWhadb /etc/opt/SUNWhadb /var/opt/SUNWhadb
chown -R $OWNER /u0/HADB-repos

exit 0

